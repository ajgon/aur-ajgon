# Maintainer: Igor Rzegocki
pkgname=dmenu-xft-height-fuzzy-history
pkgver=5.0
pkgrel=1
pkgdesc="Dynamic X menu - with xft, height, history, and fuzzy search support"
url="http://tools.suckless.org/dmenu/"
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft')
conflicts=('dmenu' 'dmenu2')
provides=('dmenu')
patches=(dmenu-history-fixed-4.9.diff
         dmenu-fuzzymatch-4.9.diff
         dmenu-lineheight-4.9.diff)
source=(http://dl.suckless.org/tools/dmenu-$pkgver.tar.gz "${patches[@]}")
sha256sums=('fe18e142c4dbcf71ba5757dbbdea93b1c67d58fc206fc116664f4336deef6ed3'
            '480dae5b4d18198d887c8ea02758e03d55da04db5a6afb43f086e68f75a41b4c'
            'd9a1e759cd518348fc37c2c83fbd097232098562ebfd1edf85b51413ff524b79'
            'ff4af5c72908f2f504ce480ada6462c9583c31eab7cabfb690c9528e4924389a')

prepare() {
  cd $srcdir/dmenu-$pkgver
  for patch in "${patches[@]}"; do
    echo "Patching $patch"
    patch -p1 < "../${patch}"
  done
}

build() {
  cd $srcdir/dmenu-$pkgver
  make
}

package()
{
    cd "$srcdir/dmenu-$pkgver"
    make DESTDIR=$pkgdir PREFIX=/usr install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
